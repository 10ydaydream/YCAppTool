//控制组件模式和集成模式
if (rootProject.ext.isAnimApplication) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]


    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        multiDexEnabled true

        if (rootProject.ext.isAnimApplication){
            //组件模式下设置applicationId
            applicationId "com.yc.animbusiness"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //jdk1.8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (rootProject.ext.isAnimApplication) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
            jniLibs.srcDirs = ['libs']
        }
    }


    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api(rootProject.ext.dependencies["appcompat"])
    api(rootProject.ext.dependencies["annotation"])
    api(rootProject.ext.dependencies["constraintlayout"])
//    implementation project(path: ':AnimatorToolLib')
    implementation 'com.github.yangchong211.YCAppTool:AnimatorToolLib:1.3.7'
    implementation project(path: ':LibraryLayer')

    implementation project(path: ':api-manager')
    implementation project(path: ':api-compiler')
    implementation project(path: ':ServiceLoaderAnnotations')
    annotationProcessor project(path: ':api-compiler')
}
